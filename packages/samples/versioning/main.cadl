import "@cadl-lang/versioning";
import "@cadl-lang/rest";
import "./library.cadl";

@serviceTitle("Pet Store Service")
@versioned(VERSIONS)
@versionedDependency(
  Library,
  {
    v1: "1.0",
    v2: "1.1",
  }
)
namespace VersionedApi;
using Cadl.Http;

alias VERSIONS = "v1" | "v2";

model ApiVersionParam {
  @header apiVersion: VERSIONS;
}

model PetBase {
  name: string;
  favoriteToys: Library.PetToy[];
}

model Dog extends PetBase {
  type: "dog";
  nextWalkTime: zonedDateTime;

  @madeOptional("v2")
  walkerName?: string;

  @added("v2")
  commandList: string[];
}

@added("v2")
model Cat extends PetBase {
  type: "cat";
  catnipDose: int32;
}

union Pet {
  cat: Cat,
  dog: Dog,
}

@route("/")
interface MyService {
  getPet(...ApiVersionParam): OkResponse<Pet>;

  @added("v2")
  @post
  walkDog(...ApiVersionParam): OkResponse<{}>;

  @removed("v2")
  @post
  walkCat(...ApiVersionParam): OkResponse<{}>;
}
